plugins {
    id 'java'
    id 'checkstyle'
    id 'com.diffplug.spotless' version '6.21.0'
}

checkstyle {
    toolVersion = '10.12.1'
    config = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml')
}

spotless {
    java {
        googleJavaFormat() // Aplica formato Google Java Style
    }
}


group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // --- JUnit 5 ---
    testImplementation platform("org.junit:junit-bom:5.10.2") // usar la última 5.10.x
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"


    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
    testCompileOnly 'org.projectlombok:lombok:1.18.38'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.38'

    // JPA (Spring Data)
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.5.3'

    // Para tests de integración (si necesitás Spring Boot Test en el futuro)
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.5.3'
}

test {
    useJUnitPlatform()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
            runtimeClasspath += output + compileClasspath
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

task integrationTest(type: Test) {
    description = "Ejecuta los tests de integración"
    group = "verification"
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
    useJUnitPlatform()
}

tasks.register('checkstyleAll') {
    group = 'verification'
    description = 'Corre Checkstyle en todo el código'
    dependsOn 'checkstyleMain', 'checkstyleTest'
}


check.dependsOn integrationTest
